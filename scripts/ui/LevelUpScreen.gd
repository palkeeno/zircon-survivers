extends CanvasLayer

@onready var buttons_container = $Control/VBoxContainer/ButtonsContainer
@onready var title_label = $Control/VBoxContainer/Title

var upgrades = [
	# Legacy placeholder list (unused). Choices are now generated by C# LoadoutManager.
]

func _ready():
	process_mode = Node.PROCESS_MODE_ALWAYS # Must run while paused
	visible = false
	
	if has_node("/root/GameManager"):
		get_node("/root/GameManager").level_up_choice_requested.connect(_on_level_up_requested)

func _on_level_up_requested():
	visible = true
	_generate_choices()

func _generate_choices():
	# Clean up old buttons
	for child in buttons_container.get_children():
		child.queue_free()

	var lm = _get_loadout_manager()
	var offers = []
	if lm:
		offers = lm.call("GenerateOffers", 4)
	
	for offer in offers:
		var btn = Button.new()
		btn.custom_minimum_size = Vector2(0, 80)
		
		# Build content: [icon 32x32] [name + desc]
		var hbox := HBoxContainer.new()
		hbox.add_theme_constant_override("separation", 8)
		
		# Only add icon if one is available
		var icon_path := str(offer.get("icon_path", ""))
		if icon_path != "":
			var texrect := TextureRect.new()
			texrect.expand_mode = TextureRect.EXPAND_IGNORE_SIZE
			texrect.custom_minimum_size = Vector2(40, 40)
			texrect.size_flags_horizontal = Control.SIZE_SHRINK_CENTER
			texrect.size_flags_vertical = Control.SIZE_SHRINK_CENTER
			texrect.stretch_mode = TextureRect.STRETCH_KEEP_ASPECT_CENTERED
			var tex: Texture2D = load(icon_path)
			if tex:
				texrect.texture = tex
			hbox.add_child(texrect)
		
		# Text content (name + description)
		var vbox := VBoxContainer.new()
		vbox.size_flags_horizontal = Control.SIZE_EXPAND_FILL
		vbox.custom_minimum_size = Vector2(300, 0)
		
		var name_label := Label.new()
		name_label.text = str(offer.get("name", ""))
		name_label.custom_minimum_size = Vector2(300, 0)
		
		var desc_label := Label.new()
		desc_label.text = str(offer.get("desc", ""))
		desc_label.autowrap_mode = TextServer.AutowrapMode.AUTOWRAP_WORD
		desc_label.custom_minimum_size = Vector2(300, 0)
		
		vbox.add_child(name_label)
		vbox.add_child(desc_label)
		hbox.add_child(vbox)
		
		btn.add_child(hbox)

		var offer_id = str(offer.get("offer_id", ""))
		btn.pressed.connect(func(): _apply_offer(offer_id))
		buttons_container.add_child(btn)


func _get_loadout_manager() -> Node:
	if not has_node("/root/GameManager"):
		return null
	var gm = get_node("/root/GameManager")
	var player = gm.player_reference
	if not player:
		return null
	if player.has_node("LoadoutManager"):
		return player.get_node("LoadoutManager")
	return null


func _apply_offer(offer_id: String):
	var lm = _get_loadout_manager()
	if lm and offer_id != "":
		lm.call("ApplyOffer", offer_id)

	# Resume Game
	visible = false
	get_tree().paused = false
	if has_node("/root/GameManager"):
		get_node("/root/GameManager").resume_game()
